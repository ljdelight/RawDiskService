buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.netflix.nebula:gradle-ospackage-plugin:4.0.0'
    }
}

apply plugin: 'nebula.deb'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'findbugs'

repositories {
    mavenCentral()
}

findbugs {
    ignoreFailures = true
    effort = "max"
    reportLevel = "low"
    excludeFilter = file("$rootProject.projectDir/config/findbugs/excludeFilter.xml")
}

dependencies {
    compile "org.apache.logging.log4j:log4j-api:2+"
    compile "org.apache.logging.log4j:log4j-core:2+"
    compile "org.apache.thrift:libthrift:0.9+"
    compile "org.slf4j:slf4j-simple:1.7+"
    testCompile "junit:junit:4.12+"
}

task debpackage(type: Deb) {
    packageName = 'ljdelight-rawdisk'
    version = '0.1.3'
    release = 1
    arch = 'amd64'
    license = 'BSL-1.0'

    // postInstall file('src/main/install/deb/postinst.sh')
    // postUninstall file('src/main/install/deb/postrm.sh')

    into '/opt/ljdelight/rawdisk'

    from(jar.outputs.files) {
        into 'lib'
    }
    from(configurations.runtime) {
        into 'lib'
    }
    from('src/main/d/readfromdev/readfromdev') {
        fileMode 0555
        into 'lib'
    }
    from('src/main/install/deb/ljdelight-rawdisk.service')
    from('src/main/install/deb/ljdelight-rawdisk.init') { fileMode 0755 }
    from('LICENSE')

}
